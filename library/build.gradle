apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: "de.mannodermaus.android-junit5"
apply plugin: 'maven-publish'
apply plugin: 'signing'

def version = "2.7.0"

android {
    namespace 'com.suddenh4x.ratingdialog'
    compileSdk 34
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 34
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding = true
    }


    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def junitVersion = '5.10.2'

dependencies {
    implementation 'androidx.annotation:annotation:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'com.google.android.material:material:1.12.0'

//    implementation 'com.google.android.play:core:1.10.3'
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'com.google.android.play:core-ktx:1.8.1'

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.10.2"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testImplementation 'org.assertj:assertj-core:3.26.0'
    testImplementation 'io.mockk:mockk:1.13.11'
}

tasks.register('androidSourcesJar', Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                from components.release

                artifact androidSourcesJar

                groupId = 'com.suddenh4x.ratingdialog'
                artifactId = 'library'
                version = '2.8.1'

                pom {
                    name = 'Android In-App Billing v3 Library'
                    description = 'A lightweight implementation of Android In-app Billing Version 3'
                    url = 'https://github.com/SuddenH4X/awesome-app-rating'
                    packaging = 'aar'
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            id = 'SuddenH4X'
                            name = 'SuddenH4X'
                            email = 'SuddenH4X@gmail.com'
                        }
                    }
                    scm {
                        url = 'scm:git@github.com:SuddenH4X/awesome-app-rating.git'
                        connection = 'scm:git@github.com:SuddenH4X/awesome-app-rating.git'
                        developerConnection = 'scm:git@github.com:SuddenH4X/awesome-app-rating.git'
                    }
                }
            }
        }
        if (project.hasProperty('sonatypeRepo')) {
            repositories {
                maven {
                    url sonatypeRepo
                    credentials {
                        username sonatypeUsername
                        password sonatypePassword
                    }
                }
            }
        }
    }

    signing {
        required { gradle.taskGraph.hasTask("publish") }
        sign publishing.publications.maven
    }
}
